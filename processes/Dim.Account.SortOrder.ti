#region Prolog

#****Begin: Generated Statements***
#****End: Generated Statements****

#LASTCHANGE Inel Botha, 2014-09-02, Initial set up of process
#GENERALCOMMENT This process populates the 'SortOrder' attribute on the Account dimension for BI reporting purposes
#DATASOURCECOMMENT N/A
#PROLOGCOMMENT  Defines the variables, builds the appropriate subset and populates the Sort attribute for the selected hierarchy
#METADATACOMMENT N/A
#DATACOMMENT N/A
#EPILOGCOMMENT N/A

##########################################################
## Define variables                                                                                                   ##
##########################################################
sAttrDim = '}ElementAttributes_' | pDim ;
sAttrSub = 'SYS.Sort' ;
sAcctSub = 'SYS.SortOrder' ;
sProcess = '}CTK_ViewCall' ;
sCubeToClear = '}ElementAttributes_' | pDim ;
sDimToClear = '}ElementAttributes_' | pDim ;
sOpt = 'Zero' ;

If (pHierarchy@='');
  sAttrMDX = '{TM1SORT( {TM1FILTERBYPATTERN( {TM1SUBSETALL( [' | sAttrDim | '] )}, "Sort_*")}, ASC)}' ;
Else;
  sAttrMDX = '{[' | sAttrDim | '].[Sort_' | pHierarchy | ']}' ;
EndIf;

##########################################################
## Create subset of attributes to be used sorted                                                        ##
##########################################################

If (SubsetExists (sAttrDim, sAttrSub) = 1) ;
   SubsetDestroy (sAttrDim, sAttrSub) ;
EndIf ;

SubsetCreateByMDX (sAttrSub, sAttrMDX);

##########################################################
## Loop through subset of Sort attributes                                                                  ##
##########################################################

nSubSize = SubsetGetSize (sAttrDim, sAttrSub) ;
x = 1 ;

While (x <= nSubSize) ;
  sSortAttr = SubsetGetElementName (sAttrDim, sAttrSub, x) ;
  sHierarchy = SubSt (sSortAttr, 6, LONG(sSortAttr)-5) ;

  sAttribute = pAttribute | '_' | sHierarchy ;

  #CLEAR EXISTING ATTRIBUTE VALUES
  sStr = sCubeToClear | ',' | sDimToClear | ',' | sAttribute ;
  ExecuteProcess (sProcess, 'pOpt', sOpt, 'pStr', sStr ) ;

  #CREATE ACCOUNT SUBSET 
  sAcctMDX = '{TM1DRILLDOWNMEMBER( {[' | pDim | '].[' | sHierarchy | ']}, ALL, RECURSIVE )}' ; 
  If (SubsetExists (pDim, sAcctSub) = 1) ;
    SubsetDestroy (pDim, sAcctSub) ;
  EndIf ;

  SubsetCreateByMDX (sAcctSub, sAcctMDX);
  SubsetExpandAboveSet(pDim, sAcctSub, 1) ;

  #FLAG EACH ACCOUNT WITH A NUMBER FOR SORT ORDER WHILE CYCLING THROUGH ACCOUNT SUBSET
  ySubSize = SubsetGetSize (pDim, sAcctSub) ;
  y = 1 ;

  While (y <= ySubSize) ;
    sAccount = SubsetGetElementName (pDim, sAcctSub, y) ;
    AttrPutN (y, pDim, sAccount, sAttribute) ;
    y = y + 1 ;
  End ;

  x = x + 1;
End;


#endregion
#region Metadata

#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion
#region Data

#****Begin: Generated Statements***
#****End: Generated Statements****

#endregion
#region Epilog

#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion