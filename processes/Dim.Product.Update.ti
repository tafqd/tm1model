#region Prolog

#****Begin: Generated Statements***
#****End: Generated Statements****

#LASTCHANGE Inel Peacock, 2014-11-18, Initial set up of process
#GENERALCOMMENT Update Product Dimension
#DATASOURCECOMMENT CSV file
#PROLOGCOMMENT Update Account Dimension
#METADATACOMMENT N/A
#DATACOMMENT N/A
#EPILOGCOMMENT N/A

#################################
## Define variables                                         ##
#################################

sDimName = 'Product' ;
sAttr = 'Code and Description' ;
sTopConsol = 'Product Consolidations' ;
sReportConsol = 'Reporting' ;
sSysConsol = 'System' ;
sAllConsol = 'All Products' ;
sUnspecProd = 'Unspec Product' ;
sNoProd = 'No Product' ;
sToAllocate = 'All ' | sDimName |' Codes';
sInputConsol = 'All Inputs' ;

sMeasAttr = 'Unit measure' ;
sCatAttr = 'Product category' ;
sAliasAttr = 'Code and Description' ;

# Hierarchy Attibutes required
sWeightPrefix = 'Weight_';
sHierarchyPrefix = 'Hierarchy_';

sTopHierarchyAttr = sHierarchyPrefix | sAllConsol;
sTopWeightAttr = sWeightPrefix | sAllConsol;


#########################################################
## Disable cube logging for bulk data loads - performance improvements              ##
#########################################################
sCube = '}ElementAttributes_Product';
sCubeLogging = CubeGetLogChanges(sCube);
CubeSetLogChanges(sCube, 0);

##################################
## Unwind to be allocated Consolidation          ##
##################################
If(DimIx(sDimName, sToAllocate) > 0);
  sProcessName = '}CTK_DisconnectConsolidations';
  ExecuteProcess(sProcessName, 'pDimName', sDimName, 'pConsolidation', sToAllocate);
EndIf;

##########################################
## Insert Attributes                                                               ##
##########################################
sProcessName = '}CTK_CreateAttributes';
ExecuteProcess(sProcessName,'pDimName',sDimName);

# Insert Hierarchy Attributes Required
AttrInsert(sDimName,'', sTopHierarchyAttr,'S');
AttrInsert(sDimName,'', sTopWeightAttr,'N');
AttrInsert(sDimName,'',sMeasAttr,'S');
AttrInsert(sDimName,'',sCatAttr,'S');


####################################################
## Add system elements into dimension and hierarchy                                 ##
####################################################
# All Product Consolidations consolidations
If (Dimix (sDimName, sTopConsol)=0);
  DimensionElementInsertDirect (sDimName, '', sTopConsol, 'C');
EndIf ;

# Reporting Consolidation
If (Dimix (sDimName, sReportConsol)=0);
  DimensionElementInsertDirect (sDimName, '', sReportConsol, 'C');
EndIf ;

DimensionElementComponentAdd (sDimName, sTopConsol, sReportConsol, 0);

# System Consolidation
If (Dimix (sDimName, sSYSConsol)=0);
  DimensionElementInsertDirect (sDimName, '', sSysConsol, 'C');
EndIf ;

DimensionElementComponentAdd (sDimName, sTopConsol, sSysConsol, 0);

# All Products Consolidation
If (Dimix (sDimName, sAllConsol)=0);
  DimensionElementInsertDirect (sDimName, '', sAllConsol, 'C');
EndIf ;

DimensionElementComponentAdd (sDimName, sReportConsol, sAllConsol, 0) ;

# Unspec Element
If (Dimix (sDimName, sUnspecProd)=0);
  DimensionElementInsertDirect (sDimName, '', sUnspecProd, 'N');
EndIf ;

DimensionElementComponentAdd (sDimName, sAllConsol, sUnspecProd, 1);

# No Product Element
If (Dimix (sDimName, sNoProd)=0);
  DimensionElementInsertDirect (sDimName, '', sNoProd, 'N');
EndIf ;

DimensionElementComponentAdd (sDimName, sSysConsol, sNoProd, 1);

# All Product Codes consolidation
If (Dimix (sDimName, sToAllocate) =0 );
  DimensionElementInsertDirect(sDimName,'',sToAllocate,'C');
Endif;

DimensionElementComponentAddDirect(sDimName,sSysConsol,sToAllocate,1);

# All Inputs consolidation
If (Dimix (sDimName, sInputConsol) =0 );
  DimensionElementInsertDirect(sDimName,'',sInputConsol,'C');
Endif;

DimensionElementComponentAddDirect(sDimName,sSysConsol,sInputConsol,1);
#endregion
#region Metadata

#****Begin: Generated Statements***
#****End: Generated Statements****

######################################
## Define variables                                                     ##
######################################

#Category inputs for assumptions to be entered against
sInputCat = vProductCat | '.Input' ;

######################################
## Insert Element and connnect to parent                  ##
######################################

If(DimIx(sDimName, vProduct) = 0);
  DimensionElementInsert(sDimName,'',vProduct,'N');
EndIf;

DimensionElementComponentAdd(sDimName,sToAllocate,vProduct,1);

If(DimIx(sDimName, sInputCat) = 0);
  DimensionElementInsert(sDimName,'',sInputCat,'N');
EndIf;

DimensionElementComponentAdd(sDimName,sInputConsol,sInputCat,1);
#endregion
#region Data

#****Begin: Generated Statements***
#****End: Generated Statements****

#################################
## Insert Attributes                                          ##
#################################
# Generic attributes etc
AttrPutS(vUnit, sDimName, vProduct, sMeasAttr);
AttrPutS(vProductCat, sDimName, vProduct, sCatAttr) ;

# Hierarchy Attributes
AttrPutS(vProductCat , sDimName, vProduct, sTopHierarchyAttr);
AttrPutN(0, sDimName, vProduct, sTopWeightAttr);

# Alias for input elements only
sInputCat = vProductCat | '.Input' ;
sCodeDesc = vProductCat | ' (Category)' ;
AttrPutS (sCodeDesc, sDimName, sInputCat, sAliasAttr) ;

#endregion
#region Epilog

#****Begin: Generated Statements***
#****End: Generated Statements****

################################
## Rebuild Hierarchy                                    ##
################################
sProcessName = 'Dim.Generic.Hierarchy';
ExecuteProcess(sProcessName,'pDimName',sDimName, 'pHierarchy', sTopHierarchyAttr);

#ProcessQuit; 

################################
## Delete any empty consolidations             ##
################################
sProcessName = '}CTK_EmptyConsolidationsDelete';
ExecuteProcess(sProcessName,'pDimName',sDimName);


###########################################################
## Check for Orphan elements and add them to a orphan elements consolidation      ##
###########################################################
sProcessName = '}CTK_CheckOrphanElements';
ExecuteProcess(sProcessName,'pDimName',sDimName);


################################
## Set cube logging to original value            ##
################################
CubeSetLogChanges(sCube, sCubeLogging);
#endregion