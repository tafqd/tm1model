#region Prolog

#****Begin: Generated Statements***
#****End: Generated Statements****

#LASTCHANGE Louise Davenport 9-Oct-2015 Initial Build of Process
#GENERALCOMMENT Add additional TBA elements to List Dimensions - Personnel and Capex
#DATASOURCECOMMENT None
#PROLOGCOMMENT Update Entity Dimension
#METADATACOMMENT N/A
#DATACOMMENT N/A
#EPILOGCOMMENT N/A


##################################
## Set Constants and Variables                         ##
##################################
# List Dimension name is Cube Name with List after it
s_DimName = pCube | ' List';
s_TBAPrefix = 'TBA ';
s_Attr = 'Caption';
s_OrderAttr = 'Order Override';
s_CapexHierAttr = 'Hierarchy_All Capex Lists';


# Determine Consol to add the TBA's to
If(pCube @= 'Personnel');
  s_Consol = 'New Employees';
ElseIf(pCube @= 'Capex');
  s_Consol = 'Total Capex';
Else;
  # Quit the process for the moment
  ProcessQuit;
EndIf;

# Determine Number of Existing TBA's
n_Existing = ElCompN(s_DimName, s_Consol);
n_LoopCtrl = n_Existing + 1;
n_LoopMax = n_Existing + pNoToAdd;

While(n_LoopCtrl <= n_LoopMax);
  s_Element = s_TBAPrefix | NumberToString(n_LoopCtrl);
  # Add the Element
  DimensionElementInsert(s_DimName, '', s_Element, 'N');
  # Add the Element to the Consol
  DimensionElementComponentAdd(s_DimName, s_Consol, s_Element, 1);

  n_LoopCtrl = n_LoopCtrl + 1;
End;
#endregion
#region Metadata

#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion
#region Data

#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion
#region Epilog

#****Begin: Generated Statements***
#****End: Generated Statements****

# Add Caption Attribute to Capex list
# Also Add Hierarchy and Order override
If(pCube @= 'Capex');
  n_LoopCtrl = n_Existing + 1;
  n_LoopMax = n_Existing + pNoToAdd;

  While(n_LoopCtrl <= n_LoopMax);
    s_Number = NumberToString(n_LoopCtrl);
    s_Element = s_TBAPrefix | s_Number;

    # Add the Attribute
    AttrPutS(s_Number,  s_DimName, s_Element, s_Attr);
    AttrPutS(s_Consol,  s_DimName, s_Element, s_CapexHierAttr);
    AttrPutN(n_LoopCtrl,  s_DimName, s_Element, s_OrderAttr);


    n_LoopCtrl = n_LoopCtrl + 1;
  End;


EndIf;
#endregion