FEEDSTRINGS;
SKIPCHECK;

[{'#DocumentElements','#ShowUndocumentedElements'}] = S:STET;

#Name Data Validation & Automation rules
#Desc Apply rules to direct user to the correct place to enter data and to automate the creation of applications
#Date 01/10/2015
#Author The QUBEdocs Team

[] = S:
IF(ELISCOMP('QUBEdocs Definition',!QUBEdocs Definition,'All Definitions') =1,
'',CONTINUE);

['#AppGroup'] = S:
IF(Db('QubeDocs Definitions',!QubeDocs Application,!QubeDocs Definition,'#IncludeInApplication') @<>'YES', STET,CONTINUE);

['#AppGroup'] = S:
IF(Db('QubeDocs Definitions',!QubeDocs Application,!QubeDocs Definition,'#CustomAppGroup') @<> '',
   Db('QubeDocs Definitions',!QubeDocs Application,!QubeDocs Definition,'#CustomAppGroup')
,CONTINUE);

['#AppGroup'] = S:
IF(Attrs('QubeDocs Definition',!QubeDocs Definition,'Type') @= 'SecurityUser','Security Users'
,CONTINUE);

['#AppGroup'] = S:
IF(Attrs('QubeDocs Definition',!QubeDocs Definition,'Type') @= 'SecurityGroup','Security Groups'
,CONTINUE);

['#AppGroup'] = S:
IF(Attrs('QubeDocs Definition',!QubeDocs Definition,'Type') @= 'Folder' % Attrs('QubeDocs Definition',!QubeDocs Definition,'Type') @= 'Document'
% Attrs('QubeDocs Definition',!QubeDocs Definition,'Type') @= 'SecurityUser' % Attrs('QubeDocs Definition',!QubeDocs Definition,'Type') @= 'SecurityGroup',
Attrs('QubeDocs Definition',!QubeDocs Definition,'Type') | 's',
'AUTO');

[{'#CustomAppGroup','#IncludeInApplication'}] = S:
IF(Attrs('QubeDocs Definition',!QubeDocs Definition,'Element') @<> '' % Attrs('QubeDocs Definition',!QubeDocs Definition,'Type') @= 'Folder',
 IF(Db('QubeDocs Definitions',!QubeDocs Application,ELPAR('QubeDocs Definition',!QubeDocs Definition,1),!QubeDocs Measure) @<> '',
Db('QubeDocs Definitions',!QubeDocs Application,ELPAR('QubeDocs Definition',!QubeDocs Definition,1),!QubeDocs Measure),CONTINUE)
,STET);

[{'#CustomAppGroup','#IncludeInApplication','#AppGroup'}]=S:STET;

['#IncludeInApplicationAs','Entire Model'] = S:STET;

['#Indent'] = S:
IF(ELISANC('QubeDocs Definition','All Folders',!QUBEdocs Definition)<>1,STET,CONTINUE);
['#Indent'] = S:
IF(Db('QubeDocs Definitions',!QubeDocs Application,!QubeDocs Definition,'#IncludeInApplication')@<>'YES',STET,CONTINUE);

['#Indent'] = S:
 IF(Db('QubeDocs Definitions',!QubeDocs Application,DIMNM('QubeDocs Definition',DIMIX('QubeDocs Definition',!QubeDocs Definition)-1),'#IncludeInApplication')@='YES' & ELLEV('QubeDocs Definition',DIMNM('QubeDocs Definition',DIMIX('QubeDocs Definition',!QubeDocs Definition)-1)) >ELLEV('QubeDocs Definition',!QubeDocs Definition),
'..' | Db('QubeDocs Definitions',!QubeDocs Application,DIMNM('QubeDocs Definition',DIMIX('QubeDocs Definition',!QubeDocs Definition)-1),'#Indent')
,
 IF(Db('QubeDocs Definitions',!QubeDocs Application,DIMNM('QubeDocs Definition',DIMIX('QubeDocs Definition',!QubeDocs Definition)-1),'#IncludeInApplication')@='YES' & ELLEV('QubeDocs Definition',DIMNM('QubeDocs Definition',DIMIX('QubeDocs Definition',!QubeDocs Definition)-1)) <ELLEV('QubeDocs Definition',!QubeDocs Definition),
DELET(Db('QubeDocs Definitions',!QubeDocs Application,DIMNM('QubeDocs Definition',DIMIX('QubeDocs Definition',!QubeDocs Definition)-1),'#Indent'),1,2)
,
Db('QubeDocs Definitions',!QubeDocs Application,DIMNM('QubeDocs Definition',DIMIX('QubeDocs Definition',!QubeDocs Definition)-1),'#Indent'))
);

['Entire Model'] = S:
IF((Isleaf=1) % ELISCOMP('QUBEdocs Definition',!QUBEdocs Definition,'All Dimensions') = 1 %  ELISCOMP('QUBEdocs Definition',!QUBEdocs Definition,'All Cubes') = 1
%  ELISANC('QUBEdocs Definition','All Folders',!QUBEdocs Definition) = 1,
 CONTINUE,
 ''
);

['#Desc','ENTIRE MODEL'] = S:STET;
['#Desc'] = S:
IF(Db('QubeDocs Definitions',!QubeDocs Application,!QubeDocs Definition,'#IncludeInApplication') @= 'YES'
& Db('QubeDocs Definitions','ENTIRE MODEL',!QubeDocs Definition,'#Desc') @<> '',
 Db('QubeDocs Definitions','ENTIRE MODEL',!QubeDocs Definition,'#Desc')
,
CONTINUE);

['#IncludeInApplicationAs'] = S:
IF(Db('QubeDocs Definitions',!QubeDocs Application,!QubeDocs Definition,'#IncludeInApplication') @= 'YES',
Db('QubeDocs Definitions',!QubeDocs Application,!QubeDocs Definition,'#Indent') |
IF(Attrs('QubeDocs Definition',!QubeDocs Definition,'Element') @<> '',
 Attrs('QubeDocs Definition',!QubeDocs Definition,'Element'),
 Attrs('QubeDocs Definition',!QubeDocs Definition,'Object')
)
,CONTINUE);

['#IncludeInApplicationAs'] = S:STET;

['#Desc','Entire Model'] = S:STET;
['#Desc'] = S:
IF(Db('QubeDocs Definitions',!QubeDocs Application,!QubeDocs Definition,'#IncludeInApplication') @= 'YES',
Db('QubeDocs Definitions','ENTIRE MODEL',!QubeDocs Definition,!QubeDocs Measure), CONTINUE);


['#DocumentElements'] = S: IF(ELISCOMP('QUBEdocs Definition',!QUBEdocs Definition,'All Dimensions') = 1,
 CONTINUE,
 ''
);


['#DocumentName','Entire Model'] = S:
IF(Attrs('QubeDocs Definition',!QubeDocs Definition,'Type') @= 'Document' & subst(!QUBEdocs Definition,1,4) @='OTH:',
 Attrs('QubeDocs Definition',!QubeDocs Definition,'OBJECT')
,STET);

['Entire Model'] = S: STET;

[{'#DocumentName','#DocumentLink'}] = S:
IF(Db('QubeDocs Definitions','Entire Model',!QubeDocs Definition,!QubeDocs Measure) @<> '',
 Db('QubeDocs Definitions','Entire Model',!QubeDocs Definition,!QubeDocs Measure)
, CONTINUE);


FEEDERS;
['#IncludeInApplication'] =>['#IncludeInApplicationAs'];
['#IncludeInApplication'] =>['#Desc'],['#AppGroup'],['#CustomAppGroup'];