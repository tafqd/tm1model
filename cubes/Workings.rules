#Region System
FEEDSTRINGS;
SKIPCHECK;
#EndRegion

#Region STETS
#Name Dynamic version and actual limit
#Desc Limits subsequent rules to dynamic versions including Actual
#Date 20/05/2014
#Author Louise Davenport
#Group Rule Control
[] = N: IF (ELISANC('Version','Dynamic Versions and Actual',!Version)=1,CONTINUE,STET);


#Name Dynamic version limit
#Desc Limits subsequent rules to dynamic versions
#Date 20/05/2014
#Author Louise Davenport
[] = N: IF (ELISANC('Version','Dynamic Versions',!Version)=1,CONTINUE,STET);

#Name STET actual months
#Desc Limits rules to all months that have not been actualised. Actuals are populated by a Turbo Integrator process.
#Date 20/05/2014
#Author Louise Davenport
[] = N:
IF (DB('Assumptions',!Version,!Period,'No Entity','No Currency','Month Contains Actuals','Value') =1,
	STET,
Continue);
#EndRegion

[{'Quantity','Rate'}] = N: STET;

['Amount']= N:
IF(Subst(!Period,5,6) @=  '.Input',
  IF(['Quantity']<>0 & ['Rate']<>0,
    ['Quantity'] *['Rate'],
  Continue),
CONTINUE);

#Region Currency Conversion
#Name Convert Currency to local currency
#Desc Convert all amounts planned in other currencies currencies to the local currency.
#Date 20/05/2014
#Author Louise Davenport
[ReportType: 'Converted','Amount']= N:
	# Don't make them have to enter a 1 when there is only one currency and populate into the reporting Currency
	 IF(!Currency @= DB('Assumptions',								 'No Version','No Period', 'No Entity', 'No Currency', 'Reporting Currency', 'String'),
		['Local'],
		['Local']
		 \ DB('Assumptions', !Version,!Period, 'No Entity', !Currency, 'Exchange Rate', 'Value')
	 );

[ReportType:'Group','Amount']= N:
	# Don't make them have to enter a 1 when there is only one currency and populate into the reporting Currency
	 IF(!Currency @= DB('Assumptions', 'No Version','No Period', 'No Entity', 'No Currency', 'Reporting Currency', 'String'),
		['All Currencies','Converted'],
	 CONTINUE);

# Rule to stet other than local below??
['Amount']= N:
	IF(!ReportType @= 'Local',
		Continue,
	STET);

#EndRegion

#Region Phasing
#Name Phasing drivers
#Desc Phase input amounts based on a driver
#Date 20/05/2014
#Author Michael Mackie
[ReportType:'Local','Amount'] = N:
	IF(ELISANC('Period','All FYs',!Period)=1,CONTINUE
	,STET);

##Last Year
# Autobalancing
[ReportType:'Local','Amount'] = N:
	IF(DB('Workings',!Version, SUBST(!Period,1,4) | '.Input', !Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account, !Workings Line, 'Driver') @='Last Year',
		# If Auto Balancing then
		If(DB('Assumptions', !Version, 'No Period', 'No Entity', 'No Currency', 'Phasing Auto Balance', 'Value')> 0,
			DB('PL',!Version,ATTRS('Period',!Period,'Previous Year') | SUBST(!Period,5,3),!Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account,!Workings Measure)
			 \
			DB('PL',!Version,ELCOMP('Period',ATTRS('Period',!Period,'Previous Year') | '.YTG',DB('Assumptions',!Version,SUBST(!Period,1,4)|'.FY','No Entity','No Currency','Month Contains Actuals','Value')),!Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account,!Workings Measure)
			*
		# Find out amount remaining for the year
		(DB('Workings',!Version,SUBST(!Period,1,4) | '.Input', !Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account, !Workings Line,!Workings Measure)-
		DB('Workings',!Version,ELCOMP('Period',SUBST(!Period,1,4) | '.YTD',DB('Assumptions',!Version,SUBST(!Period,1,4)|'.FY','No Entity','No Currency','Month Contains Actuals','Value')),
			!Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account, !Workings Line,!Workings Measure)),
		Continue)
	,CONTINUE);
# Last Year Non Auto Balancing
[ReportType:'Local','Amount'] = N:
	IF(DB('Workings',!Version, SUBST(!Period,1,4) | '.Input', !Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account, !Workings Line, 'Driver') @='Last Year',
			DB('PL',!Version,ATTRS('Period',!Period,'Previous Year') | SUBST(!Period,5,3),!Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account,!Workings Measure)
			 \
			DB('PL',!Version,ELCOMP('Period',ATTRS('Period',!Period,'Previous Year') | '.YTG',DB('Assumptions',!Version,SUBST(!Period,1,4)|'.FY','No Entity','No Currency','Month Contains Actuals','Value')),!Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account,!Workings Measure)
			*
		# Determine whether Auto-balance to full year amount or not
		DB('Workings',!Version,SUBST(!Period,1,4) | '.Input', !Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account, !Workings Line,!Workings Measure)
	,CONTINUE);

#Phasing Profiles
# Look to see if a version specific profile otherwise use No Version
[ReportType:'Local','Amount'] = N:
	IF(DB('Workings',!Version, SUBST(!Period,1,4) | '.Input', !Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account, !Workings Line, 'Driver') @<>'',
		If(DB('Assumptions', !Version, 'No Period', 'No Entity', 'No Currency', 'Phasing Auto Balance', 'Value')> 0,
		# If Auto Balancing then
		DB('Phasing Profiles', !Version, !Period,
		DB('Workings',!Version,SUBST(!Period,1,4) | '.Input',!Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account, !Workings Line, 'Driver'),'Rephased %')
		*
		# Adjust Input Amount down by YTD actuals
		(DB('Workings',!Version,SUBST(!Period,1,4) | '.Input', !Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account, !Workings Line,!Workings Measure) -
			DB('Workings',!Version,ELCOMP('Period',SUBST(!Period,1,4) | '.YTD',DB('Assumptions',!Version,SUBST(!Period,1,4)|'.FY','No Entity','No Currency','Month Contains Actuals','Value')),
			!Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account, !Workings Line,!Workings Measure)),
		Continue)
	,Continue);
# If Not Autobalancing then phase input amount out into remaining months based on phasing driver - Rephased % can be used for the specific version
[ReportType:'Local','Amount'] = N:
	IF(DB('Workings',!Version, SUBST(!Period,1,4) | '.Input', !Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account, !Workings Line, 'Driver') @<>'',
		DB('Phasing Profiles', !Version, !Period,
		DB('Workings',!Version,SUBST(!Period,1,4) | '.Input',!Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account, !Workings Line, 'Driver'),'Rephased %') *
		DB('Workings',!Version,SUBST(!Period,1,4) | '.Input', !Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account, !Workings Line,!Workings Measure)
	,Continue);
# If not User Defined then look at "Default Phasing" attribute
[ReportType:'Local','Amount'] = N:
	IF(AttrS('Account', !Account, 'Default Phasing') @<>'',
		Continue
	,Stet);
[ReportType:'Local','Amount'] = N:
		IF(AttrS('Account', !Account, 'Default Phasing') @='Last Year',
		# If Auto Balancing then
			If(DB('Assumptions', !Version, 'No Period', 'No Entity', 'No Currency', 'Phasing Auto Balance', 'Value')> 0,
				DB('PL',!Version,ATTRS('Period',!Period,'Previous Year') | SUBST(!Period,5,3),!Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account,!Workings Measure)
				 \
				DB('PL',!Version,ELCOMP('Period',ATTRS('Period',!Period,'Previous Year') | '.YTG',DB('Assumptions',!Version,SUBST(!Period,1,4)|'.FY','No Entity','No Currency','Month Contains Actuals','Value')),!Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account,!Workings Measure)
				*
				# Find out amount remaining for the year
				(DB('Workings',!Version,SUBST(!Period,1,4) | '.Input', !Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account, !Workings Line,!Workings Measure)-
				DB('Workings',!Version,ELCOMP('Period',SUBST(!Period,1,4) | '.YTD',DB('Assumptions',!Version,SUBST(!Period,1,4)|'.FY','No Entity','No Currency','Month Contains Actuals','Value')),
				!Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account, !Workings Line,!Workings Measure)),
		# Last Year Non Auto Balancing
				DB('PL',!Version,ATTRS('Period',!Period,'Previous Year') | SUBST(!Period,5,3),!Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account,!Workings Measure)
				 \
				DB('PL',!Version,ELCOMP('Period',ATTRS('Period',!Period,'Previous Year') | '.YTG',DB('Assumptions',!Version,SUBST(!Period,1,4)|'.FY','No Entity','No Currency','Month Contains Actuals','Value')),!Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account,!Workings Measure)
				*
			# Determine whether Auto-balance to full year amount or not
			DB('Workings',!Version,SUBST(!Period,1,4) | '.Input', !Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account, !Workings Line,!Workings Measure))
	,CONTINUE);

#Phasing Profiles
# Look to see if a version specific profile otherwise use No Version
[ReportType:'Local','Amount'] = N:
	# If Auto Balancing then need to work out what YTD spend is and phase remaining
	If(DB('Assumptions', !Version, 'No Period', 'No Entity', 'No Currency', 'Phasing Auto Balance', 'Value')> 0,
		DB('Phasing Profiles', !Version, !Period, AttrS('Account', !Account, 'Default Phasing'),'Rephased %')		*
			(DB('Workings',!Version,SUBST(!Period,1,4) | '.Input', !Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account, !Workings Line,!Workings Measure) -
			DB('Workings',!Version,ELCOMP('Period',SUBST(!Period,1,4) | '.YTD',DB('Assumptions',!Version,SUBST(!Period,1,4)|'.FY','No Entity','No Currency','Month Contains Actuals','Value')),
			!Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account, !Workings Line,!Workings Measure)),

			# If Not Autobalancing - used Rephased % for the version in question
			DB('Phasing Profiles', !Version, !Period, AttrS('Account', !Account, 'Default Phasing'),'Rephased %') 	*
		DB('Workings',!Version,SUBST(!Period,1,4) | '.Input', !Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account, !Workings Line,!Workings Measure));



#EndRegion

FEEDERS;
#Region Feeders

#Name Feed Report Type
#Desc Feed Reporting from Local
#Date 20/05/2014
#Author Louise Davenport
#Group Feeders
['Dynamic Versions','Planning Years','All Currencies','Local']=>
	['Converted'] ;
['Dynamic Versions','Planning Years','All Currencies','Converted']=>
	DB('Workings',!Version,!Period,!Entity,!CostCentre,!Project,
	DB('Assumptions', 'No Version','No Period', 'No Entity', 'No Currency', 'Reporting Currency', 'String'),'Group',
	!Account,!Workings Line,!Workings Measure);

#Desc Feed amount when rate and quantity specified
#Date 30/10/2015
#Author Louise Davenport
['Dynamic Versions','All Input','Local',{'Rate','Quantity'}]=>
	['Amount'];

#Name Feed upstream to PL Cube
#Desc Feed PL Workings Detail numbers to PL Cube
#Date 20/05/2014
#Author Louise Davenport
['Dynamic Versions','Planning Years','Local','All Lines','Amount']=>
	DB('PL', !Version, !Period, !Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account, 'Amount');
['Dynamic Versions', 'All FYs', 'Local','All Lines','Amount']=>
	DB('PL', !Version, Subst(!Period,1,4)|'.Driver', !Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account, 'Amount');

#Name Feed Auto Phasing
#Desc Feed months when a phasing amount is specified
#Date 06/08/2014
#Author Michael Mackie
['Dynamic Versions','All Input','Local','Amount']=>
	db('Workings', !Version, SUBST(!Period,1,4) | '.FY', !Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account, !Workings Line, !Workings Measure);
['Dynamic Versions','All YTDs','Local','Amount']=>
		db('Workings', !Version, SUBST(!Period,1,4) | '.FY', !Entity, !Cost Centre, !Project, !Currency, !Report Type, !Account, !Workings Line, !Workings Measure);
#EndRegion